plugins {
    id "java"
    id "application"
    id "org.flywaydb.flyway" version "4.0.3"
}

// Use Java 8 by default
sourceCompatibility = '1.8'
targetCompatibility = '1.8'

// UTF-8 should be standard by now. So use it!
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

// The main class of the application
mainClassName = 'com.example.ExampleApplication'

// Set our project variables
project.ext {
    dropwizardVersion = '1.0.0'
}

repositories {
    mavenCentral()
}

dependencies {
    compile (
        'io.dropwizard:dropwizard-core:' + dropwizardVersion,
        'io.dropwizard:dropwizard-jdbi:' + dropwizardVersion,
        'com.h2database:h2:1.4.192'
    )
    testCompile (
        'io.dropwizard:dropwizard-testing:' + dropwizardVersion
    )
}

// Configure flyway
flyway {
    url = 'jdbc:h2:file:./persondb'
    user = 'sa'
}

// Configure the run task to start the Dropwizard service
run {
    args 'server', './config.yml'
}

test {
    testLogging {
        events "passed", "skipped", "failed", "standardOut", "standardError"
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.14.1'
}
